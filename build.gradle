plugins {
    id "com.jfrog.bintray" version "1.4"
    id "com.gradle.plugin-publish" version "0.9.1"
    id "de.kotka.project" version "1.5.1"
}

apply plugin: "java"
apply plugin: "groovy"

group = 'de.kotka.clojuresque'
version = '1.3.0-SNAPSHOT'
description = "A Clojure plugin for gradle (nrepl)"

kotka {
    wrapperVersion = "2.8"
    licenses   = [ "EUPL-1.1", "MIT" ]
    repository = "clojuresque/nrepl"
    bintray    = [ repo: "clojuresque" ]
}

repositories {
    jcenter()
}

ext.clojuresquePlugins = [
    common: "2.0.0",
    base:   "2.0.0"
]

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "de.kotka.gradle:gradle-utils:0.2.2"
    project.clojuresquePlugins.each { plugin, version ->
        compile "de.kotka.clojuresque:clojuresque-${plugin}:${version}"
    }

    testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

compileGroovy {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

pluginBundle {
    website = 'https://bitbucket.org/clojuresque/nrepl'
    vcsUrl = 'https://bitbucket.org/clojuresque/nrepl'
    description = project.description
    tags = ['clojure']

    plugins {
        nreplPlugin {
            id = 'de.kotka.clojuresque.nrepl'
            displayName = project.description
        }
    }
}
