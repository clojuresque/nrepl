buildscript {
    repositories {
        jcenter()
        maven { url "http://clojars.org/repo" }
    }

    dependencies {
        classpath "de.kotka.gradle:gradle-plugin:1.3.0"
    }
}

import kotka.gradle.KotkaPlugin

group = 'clojuresque'
version = '1.3.0-SNAPSHOT'
description = "A Clojure plugin for gradle (nrepl)"

ext.kotka = [
    repository:     "clojuresque/nrepl",
    wrapperVersion: "2.1"
]

apply {
    plugin 'java'
    plugin 'groovy'
    plugin 'kotka'
}

repositories {
    jcenter()
    maven { url "http://clojars.org/repo" }
}

ext.clojuresquePlugins = [
    common: "1.1.0",
    base:   "1.6.0"
]

uploadArchives {
    configuration = project.configurations.signatures
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "de.kotka.gradle:gradle-utils:0.1.1"
    project.clojuresquePlugins.each { plugin, version ->
        compile "clojuresque:clojuresque-${plugin}:${version}"
    }

    testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

compileGroovy {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

processResources {
    inputs.property "version", {-> project.version}
    from(sourceSets.main.resources.srcDirs) {
        expand("version": project.version)
    }
}
