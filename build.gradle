buildscript {
    repositories {
        mavenCentral()
        maven { url "http://clojars.org/repo" }
    }

    dependencies {
        classpath "de.kotka.gradle:gradle-plugin:1.2.0"
    }
}

subprojects {
    group = 'clojuresque'
    version = '1.2.0-SNAPSHOT'

    ext.kotka = [
        repository:     "clojuresque/nrepl",
        wrapperVersion: "1.10"
    ]

    apply {
        plugin 'java'
        plugin 'kotka'
    }

    repositories {
        mavenCentral()
    }

    ext.clojuresquePlugins = [
        common: "1.1.0",
        base:   "1.6.0"
    ]
}

project(':clojuresque-nrepl') {
    description = "A Clojure plugin for gradle (nrepl)"

    apply plugin: 'groovy'

    dependencies {
        compile gradleApi()
        compile localGroovy()

        compile "de.kotka.gradle:gradle-utils:0.1.1"
        project.clojuresquePlugins.each { plugin, version ->
            compile "clojuresque:clojuresque-${plugin}:${version}"
        }

        testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        }
    }

    compileGroovy {
        sourceCompatibility = "1.6"
        targetCompatibility = "1.6"
    }

    processResources {
        inputs.property "version", {-> project.version}
        from(sourceSets.main.resources.srcDirs) {
            expand("version": project.version)
        }
    }
}

project(':clojuresque-nrepl-runtime') {
    description = "A Clojure plugin for gradle (nrepl runtime files)"

    apply plugin: "java"

    dependencies {
        project.clojuresquePlugins.each { plugin, version ->
            compile "clojuresque:clojuresque-${plugin}-runtime:${version}"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.10"
}
